plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'icewizard7'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "luckperms-repo"
        url = "https://repo.luckperms.net/repo/"
    }

    maven {
        name = "EngineHub"
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    // Spigot / Paper API
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")

    // LuckPerms API
    compileOnly("net.luckperms:api:5.5")

    // Discord JDA
    implementation("net.dv8tion:JDA:5.0.0-beta.20")

    // Discord Logging
    compileOnly('org.apache.logging.log4j:log4j-core:2.25.3')
    compileOnly("org.apache.logging.log4j:log4j-api:2.19.0")

    // WorldEdit (required by WorldGuard)
    compileOnly("com.sk89q.worldedit:worldedit-bukkit:7.4.0")

    // WorldGuard
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.15")
}

shadowJar {
    archiveClassifier.set("")
}

build.dependsOn shadowJar

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
